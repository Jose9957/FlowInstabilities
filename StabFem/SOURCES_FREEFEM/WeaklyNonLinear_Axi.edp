//
//  PROGRAM WeaklyNonLinear_2D.edp
//	
//	Solves directly the Harmonic-Balance equations to decribe the limit cycle with two time-Fourier modes
//   
//
//  INPUT PARAMETERS : 
//      Normalization  (L, E or V)
//      AdjointType (cA or dA)
//      Re (Reynolds to build expansion)
//
//	INPUT FILES :
//		BaseFlow.txt	
//    EigenMode.txt
//		EigenModeA.txt 
// 		mesh.msh 			->  mesh in Freefem format
//  OUTPUT FILES :
//      WNL.ff2m 
//      HBMode1_guess.ff2m/.txt
//      HBMode2_guess.ff2m/.txt
//
// 	ERROR MANAGEMENT : 
//      if Newton iteration diverges, SelfConsistentMode.txt is not generated and "iter" parameter in Eigenmode.ff2m is -1.
//



//load "MUMPS_seq"
include "SF_Geom.edp";
include "StabFem.idp";

/////////////////////////////////////////////////////////////////////////////////
string normalisation;
cout << " Normalisation choice ?  [none, L, E or V] " << endl << "         ( V = Velocity at on point (S&L) ; E = Energy (Mantic-lugo), L = lift force )" << endl;
cin >> normalisation ;
cout << " --> " << normalisation << endl;
real Recheck;
cout << " Give a value of Reynolds number to generate 'guess'  fields for selfconsistent/HarmonicBalance  ? " 
		<< endl << "       (type -1 if you don't want this)" << endl;
cin >> Recheck;
cout << " --> " << Recheck << endl;
mesh th=readmesh(ffdatadir+"mesh.msh");                    ///// Read mesh
//int bclat=4,bcinlet=1,bcoutflow=3,bcwall=2,bcaxis=6; // no longer in the customization file. These lavels are now written explicitly

macro JJJ y //EOM
/////////////////////////////////////////////////////////////////////////////////

fespace Xh(th,P2);             ////// f.e. space for vel.
fespace Mh(th,P1);            ////// f.e. space for pressure
fespace XXMh(th,[P2,P2,P1]); ////// f.e. space for triplet [u,v,p]
fespace XXXMh(th,[P2,P2,P2,P1]); ////// f.e. space for triplet [u,v,p]

XXMh [ubx,uby,ubp]; // base flow 
XXXMh<complex> [u1Rx,u1Ry,u1Rz,u1Rp];    //eigenmode 
XXMh<complex> [u1x,u1y,u1p];    //eigenmode 
XXMh<complex> [u1Cx,u1Cy,u1Cp];    //eigenmode conjugate   
XXMh<complex> [u1Ax,u1Ay,u1Ap];    //Adjoint   
XXMh<complex> [u1ACx,u1ACy,u1ACp];    //Conjugate of Adjoint  
XXMh<complex> [Du1x,Du1y,Du1p];    //eigenmode 

XXMh [uepsx,uepsy,uepsp]; // base flow correction
XXMh<complex> [u20x,u20y,u20p], [u22x,u22y,u22p];    //terms |A|^2 and A^2


XXMh [vx,vy,vp]; // test functions
XXMh<complex> [vCx,vCy,vCp]; // test functions

real Re, ReC, Omegax, omegaC, sigmaC;

     
////////////////////////////////////////////////////////////////////////////////

macro div(u) ((dx(u#x)+dy(u#y)+(u#y)/y))   //EOM
macro Conv(ua,ub,v) ( 
              ((ua#x*dx(ub#x)+ua#y*dy(ub#x))+(ub#x*dx(ua#x)+ub#y*dy(ua#x)))*v#x 
            + ((ua#x*dx(ub#y)+ua#y*dy(ub#y))+(ub#x*dx(ua#y)+ub#y*dy(ua#y)))*v#y 
                    ) //EOM macro for mutual convection operator
macro D(u) [[dx(u#x), .5*(dx(u#y)+dy(u#x))], [.5*(dx(u#y)+dy(u#x)), dy(u#y)]] 
//EOM macro for rate-of-deformation tensor

macro NS(nu,u,v) 
		(
		 -2*nu*(D(u):D(v))
        + u#p*div(v)
        + v#p*div(u)
        - .5*Conv(u,u,v)
        )
//EOM
     
macro NSL(nu,U,u,v) 
		(
		-2*nu*(D(u):D(v))
        + u#p*div(v)
        + div(u)*v#p
        - Conv(U,u,v)
        )
//EOM
        
macro Boundaryconditions(u,v)
	           on(1,u#x=0.0,u#y=0.0) 
             + on(2,21,22,23,u#x=0.0,u#y=0.0)
             + on(6,u#y=0)
// EOM 

macro Lift(nu,u1,u2,p)   
	(
	int1d(th,2,21,22,23)(JJJ*2*p*N.y)
	+int1d(th,2,21,22,23)( -2*JJJ*nu*(2*dy(u2)*N.y+(dx(u2)+dy(u1))*N.x))
	)
//
macro Drag(nu,u1,u2,p)   
	(
	int1d(th,2,21,22,23)(2*p*JJJ*N.x)
	+int1d(th,2,21,22,23)( -2*JJJ*nu*(2*dx(u1)*N.x+(dx(u2)+dy(u1))*N.y))
	)
//


// B. Post-processing
IFMACRO(!CalcPsi)      
macro CalcPsi(psi,vort,test)
      solve LapLace(psi,test) = int2d(th)(1/y*(dx(psi)*dx(test)+dy(psi)*dy(test)))
					         - int2d(th)(vort*test)
					         + on(6,21,22,23,psi=0)
					         + on(1,4,2,psi=y^2/2);
//EOM
ENDIFMACRO      
	    
IFMACRO(!SFWriteBaseFlow)
macro SFWriteBaseFlow(filename,u,typeFlow,iter)
	{
	fespace p1forff2m(th,P1);  
    p1forff2m vort,psi,test; 
	vort=-dy(u#x)+dx(u#y);
	CalcPsi(psi,vort,test)  
	ofstream file(filename);
   	{
   	file << "### Data generated by Freefem++ ; " << endl;
    file << "BaseFlow for an axisymmetric incompressible problem" << endl;
    file << "datatype " << typeFlow << " datastoragemode ReP2P2P1.1 datadescriptors ux,ur,p,Re" << endl;
    file << "real* Re P1 vort P1 psi real iter " ; 
	string PostProcessBaseFlowDescription = " " ; 
	file << PostProcessBaseFlowDescription << endl << endl ; 
	file << Re << endl;	
	for (int j=0;j<vort[].n ; j++) file << vort[][j] << endl;
	for (int j=0;j<psi[].n ; j++)  file << psi[][j]  << endl;
	file << iter << endl << endl;
 	}
	};
//EOM
ENDIFMACRO


// C. Postprocessing
IFMACRO(!SFWriteMode)	
macro SFWriteMode(namefile,u,ev,shift,typeFlow,iter)
		 {
		 ofstream file(namefile);
   		 fespace p1forff2m(th,P1); 
    	 p1forff2m<complex> vort1;  		 
 		 file << "### Data generated by Freefem++ ; " << endl;
    	 file << " Eigenmode for Axisymmetric flow (written with generic macro)" << endl;
    	 file << "datatype " << typeFlow << "  datastoragemode CxP2P2P1.2 datadescriptors ux,ur,p,lambdar,lambdai" << endl;
    	 string descriptionFF;
    	 descriptionFF="real* Re complex* eigenvalue complex shift int iter P1c vort1"; 
	     file << descriptionFF << endl << endl ; 
		 file << Re  << endl << endl << real(ev) << " " << imag(ev) << " " ; 
		 file << real(shift) << " " << imag(shift) << " " << iter << endl << endl;
		 vort1=-dy(u#x)+dx(u#y);
		 for (int j=0;j<vort1[].n ; j++) {file << real(vort1[][j]) << " " << imag(vort1[][j]) << endl;};		
		};
//EOM	
ENDIFMACRO


{
ifstream cbin(ffdatadir+"BaseFlow.txt");
cbin >> ubx[] >> Re >> Omegax ;
cout << "Drag of guess mean flow : " << Drag(1/Re,ubx,uby,ubp) << endl;
}
real epsilonTT = sqrt(1/Re-1/Recheck); // warning may fail if subcritical ! to be fixed in due time

{
cout << "Reading Eigenmode.txt" << endl;
ifstream cbin(ffdatadir+"Eigenmode.txt");
cbin >> u1Rx[]  >> Recheck >> sigmaC >> omegaC; 
[u1x,u1y,u1p]=[u1Rx,u1Ry,u1Rp];

}


{
cout << "Reading EigenmodeA.txt" << endl;
ifstream cbin(ffdatadir+"EigenmodeA.txt");
cbin >> u1Rx[] ; 
[u1Ax,u1Ay,u1Ap]=[u1Rx,u1Ry,u1Rp];

}
u1ACx[] = conj(u1Ax[]);




if(normalisation=="none")
	{ cout << " -> No normalization for the mode " << endl;}
else if(normalisation=="L")
	{
		cout << " -> Normalisation of mode based on LIFT  " << endl;
		real Liftguess = 0.5; // normalize so that lift is 0.5 ; total lift (including cc) is 1 ; lift coef. is .5
		
		cout << "  -> Fy = " << Liftguess  << endl;

		cout << "Fy before renarmalisation : " << Lift(1/Re,u1x,u1y,u1p) << endl;
		complex modenorm = Lift(1/Re,u1x,u1y,u1p);
		u1x[] = Liftguess/modenorm*u1x[]; 
			cout << "Fy after renarmalisation : " << Lift(1/Re,u1x,u1y,u1p) << endl;	
	}	
else if(normalisation=="E")
		{
		cout << " -> Normalisation of mode based on ENERGY OF PERTURBATION (Mantic-lugo convention)  " << endl;
		real Eguess = 1;
		cout << "  -> Energy = " << Eguess << endl;

		cout << "Energy before renarmalisation : " << sqrt(4*int2d(th)(JJJ*(abs(u1x)^2+abs(u1y)^2))) << endl;
		complex modenorm = Lift(1/Re,u1x,u1y,u1p);
		u1x[] = 1/modenorm*u1x[]; // first normalize so that imag part of lift is zero
		modenorm = 2*sqrt(2*int2d(th)(JJJ*(abs(u1x)^2+abs(u1y)^2)));
		u1x[] = Eguess/modenorm*u1x[]; // then normalize so that || ums ||_L2 = 1/sqrt(2)
		cout << "Energy after renarmalisation : " << sqrt(4*int2d(th)(JJJ*(abs(u1x)^2+abs(u1y)^2))) << endl;	
		}
else if(normalisation=="V")
		{
			cout << " -> Normalisation of mode based on velocity at one point (Sipp & lebedev convention)  " << endl;
		complex Vpoint = u1y(1,0); // convention of Sipp & Lebedev
		cout << "Energy before renarmalisation : " << sqrt(4*int2d(th)(JJJ*(abs(u1x)^2+abs(u1y)^2))) << endl;
		complex modenorm = Lift(1/Re,u1x,u1y,u1p);
		u1x[] = 0.4612/Vpoint*u1x[];  // convention of Sipp & Lebedev
		cout << "Energy after renarmalisation : " << sqrt(4*int2d(th)(JJJ*(abs(u1x)^2+abs(u1y)^2))) << endl;	
		};


u1Cx[] = conj(u1x[]);

real nu = 1/Re;

/////////////////////////////////////////////////////////////////////////////////
//////// ORDER 2

cout << " Solving problems at order 2 :" << endl;

solve varfEpsilon([uepsx,uepsy,uepsp],[vx,vy,vp]) =
  	 int2d(th)( JJJ*NSL(nu,ub,ueps,v) )
	+ int2d(th) (2*JJJ*(D(ub):D(v))) 
    + Boundaryconditions(ueps,v)			
	;
	
solve varf20([u20x,u20y,u20p],[vCx,vCy,vCp]) =
  	 int2d(th)( JJJ*NSL(nu,ub,u20,vC) )
	- int2d(th)( JJJ*Conv(u1,u1C,vC) ) 
    + Boundaryconditions(u20,vC)			
	;
	
solve varf22([u22x,u22y,u22p],[vCx,vCy,vCp]) =
  	 int2d(th)( JJJ*NSL(nu,ub,u22,vC) ) -int2d(th)(JJJ*2i*omegaC*(u22x*vCx+u22y*vCy) )
	- int2d(th)(JJJ*0.5*Conv(u1,u1,vC) ) 
    + Boundaryconditions(u22,vC)			
	;

real Fx0 = Drag(nu,ubx,uby,ubp);
real Fxeps2 = (Drag(nu,uepsx,uepsy,uepsp)-Drag(1,ubx,uby,0));
real FxA20 = real(Drag(nu,u20x,u20y,u20p));
complex FxA22 = Drag(nu,u22x,u22y,u22p);
complex FyA1 = Lift(1/Re,u1x,u1y,u1p)*2;


cout << " Fx associated to order epsilon correction : " << Fxeps2 << endl;
cout << " Fx associated to order |A|^2  : " << FxA20 << endl;
cout << " Fx associated to order A^2  : " << FxA22 << endl;

/////////////////////////////////////////////////////////////////////////////////
/////// ORDER 3
cout << " Computing coeffs at order 3 :" << endl;



complex Cn;
Cn = int2d(th)(u1ACx*u1x+u1ACy*u1y);
cout << " Cn : " << Cn << endl;


complex Lambda = -1/Cn*int2d(th)(JJJ*(Conv(ueps,u1,u1AC)+2*(u1ACx*(dxx(u1x)+ .5*(dxx(u1y)+dyx(u1x))) + u1ACy*(.5*(dxy(u1y)+dyy(u1x))+ dyy(u1y))
)               ));

complex Nu0 = 1/Cn*int2d(th)(JJJ*(Conv(u20,u1,u1AC)));

complex Nu2 = 1/Cn*int2d(th)(JJJ*(Conv(u22,u1C,u1AC)));
	
cout << "Lambda : "  << Lambda << " ; Nu0 = " << Nu0 << " ; Nu2 = " << Nu2 << endl;
cout << "l1 coeff " << real(Nu0+Nu2)/omegaC << endl;
real AAA = sqrt(abs(real(Lambda)/real(Nu0+Nu2))); /// amplitude/sqrt(eps) ; value depends on normalisation choice
// Even thought it does not have sense in the subcritical case, I added the abs to be error free.

cout << endl << endl << "######### SUMMARY : ##############" << endl << endl;

cout << endl << "  >>  Amplitude :  A = " << AAA << " * epsilon^(1/2) " << endl;
cout << "  >>  In terms of Energy = " << AAA*sqrt(2*int2d(th)(JJJ*(abs(u1x)^2+abs(u1y)^2))) << " * eps " << endl;
cout << "  >>  In terms of Lift force : Fy = [" << AAA*FyA1 << " exp(i omega0 t) + c.c. ] * eps" << endl;
cout << "                             : Fy = " << real(AAA*FyA1*2) << " cos(omega0 t) + " 
                                                     << -imag(AAA*FyA1*2) << " sin(omega0 t) ] * eps" << endl;

cout << " >> Drag force  = Fx0 + Fxeps2 * eps^2 + FxA20 *|A|^2 + |A^2| * [FxA22 exp(2 i omega0 t ) + c.c. ]"  << endl;
cout << "                      = " << Fx0 << " + " << Fxeps2 << " * eps + << " << -FxA20 << " * |A|^2 + [" << FxA22 << " exp(2 i omega0 t) + c.c.] |A|^2 "  << endl << endl;
cout << "                      = Fx0 + [ Fxeps2 + CxA20*(A^2/eps^2)] * eps^2 " 
                            << " +  [ 2*Re(FxA22)*cos( 2 omega0 t ) - 2*Im(FxA22)*sin( 2 omega0 t ) ] * "  << endl;
cout << "                      = " << Fx0 << " + " << (Fxeps2 + FxA20*AAA^2) << " * eps^2 + [ " << real(2*FxA22)*AAA^2 << " * cos(2 omega0 t ) + " 
																					<< -imag(2*FxA22)*AAA^2 << " * sin(2 omega0 t ) ] * eps^2"   << endl;
         
         
cout << endl << "  >> Frequency = omega0 + im(Lambda)*eps - im(nu0+nu2)*|A|^2 "  << endl;
cout << endl << "               = " << omegaC <<  " + " << imag(Lambda) - imag(Nu0+Nu2)*AAA^2 << " * eps^2 "  << endl;
        
                                                  
real omegaNL;
if(Recheck>0)
{

cout << endl<< endl << " #####  EVALUATION FOR Re = " << Recheck << " AND GENERATION OF guesses for nonlinear approaches ############" << endl;
cout << "  >>  Amplitude :  A = " << AAA*epsilonTT  << endl;
cout << "  >>  In terms of Energy = " << AAA*sqrt(2*int2d(th)(JJJ*(abs(u1x)^2+abs(u1y)^2)))*epsilonTT << endl;
cout << "  >>  In terms of Lift force : Fy = [" << AAA*FyA1*epsilonTT << " exp(i omega0 t) + c.c. ]" << endl;
cout << "                             : Fy = " << real(AAA*FyA1)*2*epsilonTT << " cos(omega0 t) + " 
													 << -imag(AAA*FyA1)*2*epsilonTT << " sin(omega0 t) " << endl; 

cout << endl << "  >> Drag force   = " << Fx0+(Fxeps2 + FxA20*AAA^2)*epsilonTT^2 << " + [ " 
										 << real(FxA20*2)*epsilonTT^2 << " * cos(2 omega0 t ) + " 
			                             << -imag(FxA22*2)*epsilonTT^2 << " * sin(2 omega0 t ) "   << endl;

omegaNL = omegaC+(imag(Lambda) - imag(Nu0+Nu2)*AAA^2)*epsilonTT^2;
			                             
cout << endl << "  >> Frequency = " << omegaC <<  " + " << (imag(Lambda) - imag(Nu0+Nu2)*AAA^2)*epsilonTT^2  
						<<  "  =  " << omegaNL << endl ;
						
cout << endl << "########################################################" << endl << endl;
  	
};


//// GENERATE FILE WITH ALL COEFFS ; USEFUL FOR MATLAB DRIVER

	
	{
 		 ofstream file(ffdatadir+"WNL_results.ff2m");
 		 file << "### Data generated by Freefem++ ; " << endl;
    	 file << "Coefficients of the Amplitude Equation obtained by Weakly Non Linear analysis" << endl;
    	 file << "datatype WNL" << endl;
	     file << "real Rec complex eigenvalue complex Lambda complex nu0 complex nu2 real Aeps complex Fyeps" 
	          << " real Fx0 real Fxeps2 real FxA20 complex FxA22 real Fxeps20 complex Fxeps22" << endl << endl ; 
	     file << ReC << endl 
			  << 0 << endl << omegaC  << endl 
			  << real(Lambda) << endl << imag(Lambda) << endl 
			  << real(Nu0) << endl << imag(Nu0) << endl 
			  << real(Nu2) << endl << imag(Nu2) <<  endl 
			  << AAA*sqrt(2*int2d(th)(JJJ*(abs(u1x)^2+abs(u1y)^2))) << endl
			  << real(AAA*FyA1) << endl << imag(AAA*FyA1) << endl
			  << Fx0 << endl 
			  << Fxeps2 << endl
			  << FxA20 << endl  
			  << endl << real(FxA22) << endl << imag(FxA22) << endl
			  << real(FxA20*AAA^2) << endl  
			  << endl << real(FxA22*AAA^2) << endl << imag(FxA22*AAA^2) << endl ;

};



//////// GENERATE "GUESS FILES" for Self-consistent computation -> OBSOLETE ?
if(Recheck>0)
{
XXMh [umx,umy,ump]; // mean flow
Re = Recheck;

{
	ofstream file(ffdatadir+"MeanFlow_guess.txt");
		umx[] = ubx[]+epsilonTT^2*uepsx[];
	[umx,umy,ump]=[umx,umy,ump]+AAA^2*epsilonTT^2*[real(u20x),real(u20y),real(u20p)];
	file << umx[]; 
	file << endl << endl << Re;
}; 
SFWriteBaseFlow(ffdatadir+"MeanFlow_guess.ff2m",um,"MeanFlow",1);



    [u1x,u1y,u1p] = 2*AAA*epsilonTT*[u1x,u1y,u1p]; // factor 2 because (cos, sin) components instead of (re,im)
{
 	ofstream file(ffdatadir+"HBMode1_guess.txt");
   	file << u1x[] ;	
   	file << endl << endl << 0 << endl << omegaC  << endl;
};
SFWriteMode(ffdatadir+"HBMode1_guess.ff2m",u1,1i*omegaNL,1i*omegaC,"HarmonicMode",1);

	cout << 2*AAA^2*epsilonTT^2 << endl;
	u22x[] = 2*AAA^2*epsilonTT^2*u22x[]; // factor 2 because (cos, sin) components instead of (re,im)
{
 	ofstream file(ffdatadir+"HBMode2_guess.txt");
   	file << u22x[] ;	
   	file <<  endl << 0 << endl << 2*omegaC  << endl;
};

SFWriteMode(ffdatadir+"HBMode2_guess.ff2m",u22,2i*omegaNL,2i*omegaC,"SecondHarmonicMode",1);

};

	
	
	
