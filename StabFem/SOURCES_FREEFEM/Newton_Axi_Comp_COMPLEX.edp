//
//  PROGRAM Newton_Axi_Comp_COMPLEX.edp
//  computation of Axi compressible base flow [ux,uy,p,T,rho](x,y)
//   
//
//  INPUT PARAMETERS (from keyboard or pipe) :
//   	Re, Ma
//	INPUT FILES :
//		BaseFlow_guess.txt	-> guess base flow (generated by mesh generator, time integration or previous Newton)
// 		mesh.msh 			->  mesh in Freefem format
//  OUTPUT FILES :
//		BaseFlow.txt			-> base flow (FF format)
//      BaseFlow.ff2m         -> base flow (FF2M format) 
//
// 	ERROR MANAGEMENT : 
//      if Newton iteration diverges, BaseFlow.txt is not generated and "iter" parameter in BaseFlow.ff2m is -1.
//
//
// Incorporated by David Fabre from a code of Flavio
// Modified by Javier Sierra.


//////////////

load "MUMPS_FreeFem"
load "scotch";
//load "../../SOURCES_OTHER/VTK/iovtk"
include "SF_Geom.edp";
include "Param_Mapping.edp";
include "StabFem.idp";

real ts;
string ssparams = "nprow=1, npcol="+mpisize;
bool fplot=false;
int comp=0;

real Re;
real Reans,lecture;

real Ma;




if(mpirank==0)
{
	cout << " Enter Reynolds Number ?' " << endl;
	cin >> Re;

	cout << " Enter Mach Number ?' " << endl;
	cin >> Ma;

}

broadcast(processor(0),Re); 
broadcast(processor(0),Ma); 
cout << endl << " >>> RE = " << Re << "    ;  Mach = " << Ma << " MPIRANK " << mpirank <<endl;
	    
//////////////////////////////////////////////////////////////////////////////////////////////

real gamma=1.4; 
real Pr=0.7;
    real nu = 1/Re;  
    // If Nu is something else than 1/Re define a macro CUSTOMnu in your Macros_StabFem.idp file
    IFMACRO(CUSTOMnu)
    nu = CUSTOMnu;
    ENDIFMACRO// Velocity at the inlet 
//real Uinlet = (SFGeomRhole1/SFGeomRcav)^2;

mesh th;

//include "cyl_mesh.edp";
//mesh th;
if(mpirank==0)
  { 
    th=readmesh(ffdatadir+"mesh.msh");
    int[int] nupart(th.nt);
    nupart=0; 
    if(mpisize>1)
    scotch(nupart, th, mpisize);
    th=change(th,fregion= nupart[nuTriangle]);
   }
broadcast(processor(0),th); 

complex SupSemiSfera=int1d(th,bcinletsponge)(1.0/Hy(x,y,ParamMap)*Rphys(x,y,ParamMap))*(2*pi);
complex Uinlet=Mflux/(Rhoinlet*SupSemiSfera);
cout << " Mflux " << Mflux << endl;
cout << " Uinlet " << Uinlet << endl;


//////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////
//verbosity=10;
//real Re=1.0;

//////////////////////////////////////////////////////////////////////////////////////////////

real TOLLERANCENewton=1e-7;  /// tollerance of newton loop
int NITERATIONNEWTLOOP=40;  //////////////  Max number of ITERATIONs --> Newton loop
real TOLLERANCEDiv = 20.0; // Tolerance to reset initial condition due to divergence of the Newton method.

///////////////////////////////////////////////////////////////////////////////////////////////
fespace XH2(th,P2);
//fespace XH1(th,P1);
//fespace XH0(th,P0);
///////////////////////////////////////////////////////////////////////////////////////////////	
fespace XXxxxH(th,[P2,P2,P1,P1,P1]); ///   pres P1 + vel 2x P2 + temp P1 + dens P1
//fespace XXxH(th,[P2,P2,P1]);	     ///   vel P2 +press P1
macro def(u,p,T,rho)[u#x,u#y,p,T,rho]//EOF


  
XXxxxH<complex> def(du,dp,dT,drho);
XXxxxH<complex> def(Ub,Pb,Tb,Rhob);
XXxxxH<complex> def(Urif,Prif,Trif,Rhorif)=[0.0,0.0,1.0,1.0,1.0];

{
ifstream file(ffdatadir+"BaseFlow_guess.txt");
file >> Ubx[];
file >> Reans;
};

cout << "$$ Newton for Re = " << Re << endl;	
cout << "$$ Reynolds of the initial condition : " << Reans << endl;

  macro div(u) ((Dx(u#x)+Dy(u#y)+(u#y)/y))   //EOM  okok
  macro ugradf(u,f) ((u#x*Dx(f)+u#y*Dy(f)))    // okok
  macro termnonlinear(u,v,w) ((w#x*(u#x*Dx(v#x)+u#y*Dy(v#x))+w#y*(u#x*Dx(v#y)+u#y*Dy(v#y)))) //
  macro Grad(u) [[Dx(u#x),Dx(u#y),u#x-u#x],[Dy(u#x),Dy(u#y),u#x-u#x],[u#x-u#x,u#x-u#x,u#y/y]]//EOM
  macro Strain(u)[[Dx(u#x),0.5*(Dx(u#y)+Dy(u#x)),u#x-u#x],[0.5*(Dx(u#y)+Dy(u#x)),Dy(u#y),u#x-u#x],[u#x-u#x,u#x-u#x,u#y/y]]//EOM
  macro gradgrad(u,v)(Dx(u)*Dx(v)+Dy(u)*Dy(v))//EOM
  macro gradpdotv(p,v)(Dx(p)*v#x+Dy(p)*v#y)//EOM
  macro nvec(u) (N.x*u#x+N.y*u#y)//EOM
  macro tvec(u) (N.y*u#x-N.x*u#y)//EOM

varf jacobian(def(du,dp,dT,drho),def(us,ps,Ts,rhos))=int2d(th,mpirank) (JJ*(Rphys(x,y,ParamMap)*(rhos*(drho*div(Ub)+Rhob*div(du)+ugradf(Ub,drho)+ugradf(du,Rhob)) 
								       //mass equation
								       -dp*div(us)
								       //+gradpdotv(dp,us)
								       +2.0/Re*(Strain(du):Grad(us))-2.0/(3.0*Re)*(div(du)*div(us))+Rhob*termnonlinear(du,Ub,us)+drho*termnonlinear(Ub,Ub,us)+Rhob*termnonlinear(Ub,du,us)
								       //momentum
								       +Ts*(drho*ugradf(Ub,Tb)+Rhob*ugradf(Ub,dT)+Rhob*ugradf(du,Tb)+(gamma-1)*(drho*Tb*div(Ub)+Rhob*dT*div(Ub)+Rhob*Tb*div(du))-gamma*(gamma-1.0)*Ma^2/Re*(2.0*((Strain(du):Strain(Ub))+(Strain(Ub):Strain(du)))-2.0/3.0*(2.0*div(Ub)*div(du))))+gamma/(Pr*Re)*gradgrad(dT,Ts)
								       // energy
								       +ps*(gamma*Ma^2*dp-(drho*Tb+Rhob*dT))
								       )))
  +BoundaryconditionsBaseFlow(du,dp,dT,drho,Uinlet);


varf vNs(def(du,dp,dT,drho),def(us,ps,Ts,rhos))=-int2d(th,mpirank)(JJ*(Rphys(x,y,ParamMap)*(rhos*(Rhob*div(Ub)+ugradf(Ub,Rhob))
							   //mass equation
							   -Pb*div(us)
							   //+gradpdotv(Pb,us)
							   +2.0/Re*(Strain(Ub):Grad(us))-2.0/(3.0*Re)*(div(Ub)*div(us))+Rhob*termnonlinear(Ub,Ub,us)
							   //momentum
								   +Ts*(Rhob*ugradf(Ub,Tb)+(gamma-1.)*Rhob*Tb*div(Ub)-gamma*(gamma-1.0)*Ma^2/Re*(2.0*((Strain(Ub):Strain(Ub)))-2.0/3.0*(div(Ub)*div(Ub))))+gamma/(Pr*Re)*gradgrad(Tb,Ts)
							   // energy
							   +ps*(1.0+gamma*Ma^2*Pb-(Rhob*Tb))
							   )))
  +BoundaryconditionsBaseFlow(du,dp,dT,drho,Uinlet);

real res,ener=1.;
if(mpirank==0)
  {
    cout << " ................................................... "<< endl;	
    cout << " ................................................... "<< endl;	
  }
int contout=0;

  
    ///////////////////////////////////////////////////////////////////////
    
    
    

	res=1.0;
	ener=1.0;
	

	 
	  int i=0;
	  while((ener>TOLLERANCENewton)&(i<=NITERATIONNEWTLOOP))
	    {
	      ts=clock();
	      
	      matrix<complex> NewtLoc=jacobian(XXxxxH,XXxxxH);
	      matrix<complex> Newt;
	      mpiAllReduce(NewtLoc,Newt,mpiCommWorld,mpiSUM);
	      if(mpirank==0)
		cout<<"Matrix formed in "<<clock()-ts<<" s"<<endl;  
	      complex[int] rhsl= vNs(0,XXxxxH);
	      complex[int] rhs(Newt.n);
	      mpiAllReduce(rhsl,rhs,mpiCommWorld,mpiSUM);
	      set(Newt,solver=sparsesolver,sparams=ssparams);
	      dux[] =  (Newt^-1*rhs);
	      ener=sqrt(abs(int2d(th)(JJ*(Rphys(x,y,ParamMap)*(abs(duy)^2+abs(dux)^2))))); 
	      if(mpirank==0)
		{
		  cout << "iter "<<i<<" | BNorm of u = " << ener << endl;
		}
	      Ubx[]+=dux[];
	      i++;	    
	    if(ener > TOLLERANCEDiv && i <= 5)
	    {
	    	cout << "Initial condition has been reseted" << endl;
	    	Ubx[] = Urifx[];
	    }
	     }// end of NEWTON LOOP
	     
	     /// NEXT IS PLOT AND POSTPROCESS
	  
	  if(mpirank==0)
	    {
	      //cout << "p,T,rho,u,v at (5,0): " << pp(5,0) << " " << Tp(5,0) << " " << rhop(5,0) << " " << uxp(5,0) << " " << uyp(5,0) << endl;
	      cout << " -------------------------------------------------------- "<< endl;	
	      plot(Pb,fill=1,value=1,cmm="press");
	      plot(Uby,fill=1,value=1,cmm="vel: y-component");
	      plot(Ubx,fill=1,value=1,cmm="vel: x-component");
	      plot(Tb,fill=1,value=1,cmm="Temp");
	      plot(Rhob,fill=1,value=1,cmm="density");
	      cout << " Newton iteration ok!"<< endl;
	    };
	  
	  
	  
	  ////// OUTPUT FOR STABFEM  
	    
	    // sortie des r?sultats 

		if((ener>50)||(iter>Nitermax))
		{
			cout << "$$ NEWTON ITERATION FAILED" << endl;
			cout << "$$ Leaving FreeFem++ with error code 201" << endl;
			exit(201);	
		};

	

// temporary fix
XXxxxH<complex> [ux,uy,up,utemp,uro] = [(Ubx),(Uby),(Pb),(Tb),(Rhob)];
XXxxxH [urx,ury,urp,urtemp,urro] = [real(Ubx),real(Uby),real(Pb),real(Tb),real(Rhob)];

		{
		  {ofstream file(ffdatadir+"BaseFlow.txt"); // file in .txt format for FreeFem solvers
		  file << ux[]; 
		  file << endl << endl << Re;
		  }; 
         // exec("cp "+ffdatadir+"BaseFlow.txt "+ffdatadir+"BaseFlow_guess.txt"); 
         

          SFWriteBaseFlow(ffdatadir+"BaseFlow.ff2m",ur,"BaseFlow",i); // file in .ff2ml format for StabFem driver
          int[int] forder=[1,1,1,1];
		  savevtk(ffdatadir+"/BflowCM_Re"+Re+"_M"+Ma+".vtk",th,urp,[urx,ury,0],urtemp,urro,order=forder,dataname="Pb vel temp rho");
		};
          

	    
	    
	  



